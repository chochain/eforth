( example 1.      the universal greeting )

decimal

: hello cr ." hello, world!" ;

( example 2.      the big f )

: bar   cr ." *****" ;
: post  cr ." *    " ;
: f     bar post bar post post post ;

( type 'f' and a return on your keyboard, and you will see a large
f character displayed on the screen )


( example 3.      fig, forth interest group )

: center cr ."   *  " ;
: sides  cr ." *   *" ;
: triad1 cr ." * * *" ;
: triad2 cr ." **  *" ;
: triad3 cr ." *  **" ;
: triad4 cr ."  *** " ;
: quart  cr ." ** **" ;
: right  cr ." * ***" ;
: bigt  bar center center center center center center ;
: bigi  center center center center center center center ;
: bign  sides triad2 triad2 triad1 triad3 triad2 sides ;
: bigg  triad4 sides post right triad1 sides triad4 ;
: fig  f bigi bigg ;


( example 4.      repeated patterns 

for     [ index -- ]            set up loop given the index.
next    [ -- ]                  decrement index by 1.  if index<0, exit.                          if index=limit, exit loop; otherwise
                                otherwise repeat after for.
r@      [ -- index ]            return the current loop index. )

variable width                  ( number of asterisks to print )

: asterisks ( -- , print n asterisks on the screen, n=width )
        width @                 ( limit=width, initial index=0 )
        for ." *"               ( print one asterisk at a time )
        next                    ( repeat n times )
        ;

: rectangle ( height width -- , print a rectangle of asterisks )
        width !                 ( initialize width to be printed )
        for     cr
                asterisks       ( print a line of asterisks )
        next
        ;

: parallelogram ( height width -- )
        width !
        for     cr r@ spaces    ( shift the lines to the right )
                asterisks       ( print one line )
        next
        ;

: triangle ( width -- , print a triangle area with asterisks )
        for     cr
                r@ width !      ( increase width every line )
                asterisks       ( print one line )
        next
        ;

( try the following instructions: )

        3 10 rectangle
        5 18 parallelogram
        12 triangle  


( example 5.      the theory that jack built )
( this example shows you how to build a hiararchical structure in forth)

decimal
0 ucase!

: the           ." the " ;
: that          cr ." that " ;
: this          cr ." this is " the ;
: jack          ." jack builds" ;
: summary       ." summary" ;
: flaw          ." flaw" ;
: mummery       ." mummery" ;
: k             ." constant k" ;
: haze          ." krudite verbal haze" ;
: phrase        ." turn of a plausible phrase" ;
: bluff         ." chaotic confusion and bluff" ;
: stuff         ." cybernatics and stuff" ;
: theory        ." theory " jack ;
: button        ." button to start the machine" ;
: child         ." space child with brow serene" ;
: cybernatics   ." cybernatics and stuff" ;

: hiding        cr ." hiding " the flaw ;
: lay           that ." lay in " the theory ;
: based         cr ." based on " the mummery ;
: saved         that ." saved " the summary ;
: cloak         cr ." cloaking " k ;
: thick         if that else cr ." and " then
                ." thickened " the haze ;
: hung          that ." hung on " the phrase ;
: cover         if that ." covered "
                else cr ." to cover "
                then bluff ;
: make          cr ." to make with " the cybernatics ;
: pushed        cr ." who pushed " button ;
: without       cr ." without confusion, exposing the bluff" ;
: rest                                  ( pause for user interaction )
        ." . "                          ( print a period )
        10 spaces                       ( followed by 10 spaces )
(        key drop                        ( wait the user to press a key )
        cr cr ;

(
key     [ -- char ]             wait for a keystroke, and return the
                                ascii code of the key pressed.
drop    [ n -- ]                discard the number.
space   [ -- ]                  display a blank.
spaces  [ n -- ]                display n blanks.
if      [ f -- ]                if the flag is 0, skip the following
                                instructions up to else or then.  if
                                flag is not 0, execute the following
                                instructions up to else and skip to
                                then.
else    [ -- ]                  skip the following instructions
                                up to then.
then    [ -- ]                  terminate an if-else-then structure
                                or an if-then structure.
)
: cloaked cloak saved based hiding lay rest ;
: THEORY
        cr this theory rest
        this flaw lay rest
        this mummery hiding lay rest
        this summary based hiding lay rest
        this k saved based hiding lay rest
        this haze cloaked
        this bluff hung 1 thick cloaked
        this stuff 1 cover hung 0 thick cloaked
        this button make 0 cover hung 0 thick cloaked
        this child pushed
                cr ." that made with " cybernatics without hung
                cr ." and, shredding " the haze cloak
                cr ." wrecked " the summary based hiding
                cr ." and demolished " the theory rest
        ;
THEORY

( type theory to start)


( example 6.      help )
( how to use forth interpreter to carry on a dialog )

: question
        cr cr ." any more problems you want to solve?"
        cr ." what kind ( sex, job, money, health ) ?"
        cr
        ;

: help  cr
        cr ." hello!  my name is creating computer."
        cr ." hi there!"
        cr ." are you enjoying yourself here?"
        key 32 or 121 =
        cr
        if      cr ." i am glad to hear that."
        else    cr ." i am sorry about that."
                cr ." maybe we can brighten your visit a bit."
        then
        cr ." say!"
        cr ." i can solved all kinds of problems except those dealing"
        cr ." with greece. "
        question
        ;

: sex   cr cr ." is your problem too much or too little?"
        cr
        ;

: too  ;                                ( noop for syntax smoothness )

: much  cr cr ." you call that a problem?!!  i should have that problem."
        cr ." if it reall y bothers you, take a cold shower."
        question
        ;

: little
        cr cr ." why are you here!"
        cr ." you should be in tokyo or new york of amsterdam or"
        cr ." some place with some action."
        question
        ;

: health
        cr cr ." my advise to you is:"
        cr ."      1. take two tablets of aspirin."
        cr ."      2. drink plenty of fluids."
        cr ."      3. go to bed (along) ."
        question
        ;

: job   cr cr ." i can sympathize with you."
        cr ." i have to work very long every day with no pay."
        cr ." my advise to you, is to open a rental computer store."
        question
        ;

: money
        cr cr ." sorry!  i am broke too."
        cr ." why don't you sell encyclopedias of marry"
        cr ." someone rich or stop eating, so you won't "
        cr ." need so much money?"
        question
        ;

: help help ;
: h help ;
: H help ;

( type 'help' to start )


( example 7.  money exchange 

the first example we will use to demonstrate how numbers are
used in forth is a money exchange program, which converts money
represented in different currencies.  let's start with the
following currency exchange table:

        33.55 nt        1 dollar
        7.73 hk         1 dollar
        9.47 rmb        1 dollar
        1 ounce gold    285 dollars
        1 ounce silver  4.95 dollars )

decimal

: nt    ( nnt -- $ )    100 3355 */  ;
: $nt   ( $ -- nnt )    3355 100 */  ;
: rmb   ( nrmb -- $ )   100 947 */  ;
: $rmb  ( $ -- njmp )   947 100 */  ;
: hk    ( nhk -- $ )    100 773 */  ;
: $hk   ( $ -- $ )      773 100 */  ;
: gold  ( nounce -- $ ) 285 *  ;
: $gold ( $ -- nounce ) 285 /  ;
: silver ( nounce -- $ ) 495 100 */  ;
: $silver ( $ -- nounce ) 100 495 */  ;
: ounce ( n -- n, a word to improve syntax )  ;
: dollars ( n -- )      . ;

( with this set of money exchange words, we can do some tests:

        5 ounce gold .
        10 ounce silver .
        100 $nt .
        20 $rmb .

if you have many different currency bills in your wallet, you
can add then all up in dollars:

        1000 nt 500 hk + .s
        320 rmb + .s
        dollars ( print out total worth in dollars )


( example 8. temperature conversion

converting temperature readings between celcius and farenheit
is also an interesting problem.  the difference between temperature
conversion and money exchange is that the two temperature scales
have an offset besides the scaling factor. )

: f>c ( nfarenheit -- ncelcius )
        32 -
        10 18 */
        ;

: c>f ( ncelcius -- nfarenheit )
        18 10 */
        32 +
        ;

( try these commands

90 f>c .        shows the temperature in a hot summer day and
0 c>f .         shows the temperature in a cold winter night. 

in the above examples, we use the following forth arithmatic
operators:

+       [ n1 n2 -- n1+n2 ]      add n1 and n2 and leave sum on stack.
-       [ n1 n2 -- n1-n2 ]      subtract n2 from n1 and leave differrence
                                on stack.
*       [ n1 n2 -- n1*n2 ]      multiply n1 and n2 and leave product
                                on stack.
/       [ n1 n2 -- n1/n2 ]      divide n1 by n2 and leave quotient on
                                stack.
*/      [ n1 n2 n3 -- n1*n2/n3] multiply n1 and n2, divide the product
                                by n3 and leave quotient on the stack.
.s      [ ... -- ... ]          show the topmost 4 numbers on stack.
)


( example 9.  weather reporting. )

: weather ( nfarenheit -- )
        dup     55 <
        if      ." too cold!" drop
        else    85 <
                if      ." about right."
                else    ." too hot!"
                then
        then
        ;


( you can type the following instructions and get some responses from the
computer:

        90 weather too hot!
        70 weather about right.
        32 weather too cold.
)


( example 10.  print the multiplication table )

: onerow ( nrow -- )
        cr
        dup 3 .r 3 spaces
        1 11
        for     2dup *
                4 .r
                1 +
        next
        2drop ;

: multiply ( -- )
        cr cr 6 spaces
        1 11
        for     dup 4 .r 1 +
        next drop 
        1 11
        for     dup onerow 1 +
        next drop cr
        ;

( type multiply to print the multiplication table )
multiply

( example 11.  calendars )

( print weekly calendars for any month in any year. )
decimal

variable julian                         ( 0 is 1/1/1950, good until 2050 )
variable leap                           ( 1 for a leap year, 0 otherwise. )
( 1461 constant 4years                  ( number of days in 4 years )

: year ( year --, compute julian date and leap year )
        dup
        1949 - 1461 4 */mod             ( days since 1/1/1949 )
        365 - julian !                  ( 0 for 1/1/1950 )
        3 =                             ( modulus 3 for a leap year )
        if 1 else 0 then                ( leap year )
        leap !
        2000 =                          ( 2000 is not a leap year )
        if 0 leap ! then
        ;

: first ( month -- 1st, 1st of a month from jan. 1 )
        dup 1 =
        if drop 0 
        else dup 2 =
        	if drop 31 
        	else dup 3 =
        		if drop 59 leap @ + 
			else
        			4 - 30624 1000 */
        			90 + leap @ +                   ( apr. 1 to dec. 1 )
			then   ( 59/60 for mar. 1 )
		then            ( 31 for feb. 1 )
	then             ( 0 for jan. 1 )
        ;

: stars 60 for 42 emit next ;           ( form the boarder )

: header ( -- )                         ( print title bar )
        cr stars cr 
        ."    sun     mon     tue     wed     thu     fri     sat"
        cr stars cr                     ( print weekdays )
        ;

: blanks ( month -- )                   ( skip days not in this month )
        first julian @ +                ( julian date of 1st of month )
        7 mod 8 * spaces ;              ( skip colums if not sunday   )

: days ( month -- )                     ( print days in a month )
        dup first                       ( days of 1st this month )
        swap 1 + first                  ( days of 1st next month )
        over - 1 -                      ( loop to print the days )
        1 swap                          ( first day count -- )
        for  2dup + 1 -
                julian @ + 7 mod        ( which day in the week? )
                if else cr then         ( start a new line if sunday )
                dup  7 u.r              ( print day in 8 column field )
                1 +
        next
        2drop ;                         ( discard 1st day in this month )

: month ( n -- )                        ( print a month calendar )
        header dup blanks               ( print header )
        days cr stars cr ;              ( print days   )

: january       year 1 month ;
: february      year 2 month ;
: march         year 3 month ;
: april         year 4 month ;
: may           year 5 month ;
: june          year 6 month ;
: july          year 7 month ;
: august        year 8 month ;
: september     year 9 month ;
: october       year 10 month ;
: november      year 11 month ;
: december      year 12 month ;

( to print the calender of april 1999, type: )
        2021 may



( example 12.      sines and cosines

sines and cosines of angles are among the most often encountered
transdential functions, useful in drawing circles and many other
different applications.  they are usually computed using floating
numbers for accuracy and dynamic range.  however, for graphics
applications in digital systems, single integers in the range from
-32768 to 32767 are sufficient for most purposes.  we shall
study the computation of sines and cosines using the single
integers.

the value of sine or cosine of an angle lies between -1.0 and +1.0.
we choose to use the integer 10000 in decimal to represent 1.0
in the computation so that the sines and cosines can be represented
with enough precision for most applications.  pi is therefore
31416, and 90 degree angle is represented by 15708.  angles
are first reduced in to the range from -90 to +90 degrees,
and then converted to radians in the ranges from -15708 to
+15708.  from the radians we compute the values of sine and
cosine.

the sines and cosines thus computed are accurate to 1 part in
10000.  this algorithm was first published by john bumgarner
in forth dimensions, volume iv, no. 1, p. 7.

31415 constant pi
10000 constant 10k
variable xs                             ( square of scaled angle )

: kn ( n1 n2 -- n3, n3=10000-n1*x*x/n2 where x is the angle )
        xs @ swap /                     ( x*x/n2 )
        negate 10000 */                 ( -n1*x*x/n2 )
        10000 +                         ( 10000-n1*x*x/n2 )
        ;

: (sin) ( x -- sine*10k, x in radian*10k )
        dup dup 10000 */                ( x*x scaled by 10k )
        xs !                            ( save it in xs )
        10000 72 kn                     ( last term )
        42 kn 20 kn 6 kn                ( terms 3, 2, and 1 )
        10000 */                        ( times x )
        ;

: (cos) ( x -- cosine*10k, x in radian*10k )
        dup 10000 */ xs !               ( compute and save x*x )
        10000 56 kn 30 kn 12 kn 2 kn    ( serial expansion )
        ;

: sin ( degree -- sine*10k )
        31415 180 */                    ( convert to radian )
        (sin)                           ( compute sine )
        ;

: cos ( degree -- cosine*10k )
        31415 180 */
        (cos)
        ;

( to test the routines, type:

        90 sin .                         9999 
        45 sin .                         7070 
        30 sin .                         5000 
         0 sin .                            0 
        90 cos .                            0 
        45 cos .                         7071 
         0 cos .                         10000 )


( example 13.      square root

there are many ways to take the square root of an integer.  the
special routine here was first discovered by wil baden.  wil
used this routine as a programming challenge while attending
a forml conference in taiwan, 1984.

this algorithm is based on the fact that the square of n+1 is equal
to the sum of the square of n plus 2n+1.  you start with an 0 on
the stack and add to it 1, 3, 5, 7, etc., until the sum is greater
than the integer you wished to take the root.  that number when
you stopped is the square root.
)

: sqrt ( n -- root )
        1600000000 over <               ( largest square it can handle)
        if drop 40000 exit then         ( safety exit )
        >r                              ( save sqaure )
        1 1                             ( initial square and root )
        begin                           ( set n1 as the limit )
                over r@ <              ( next square )
        while
                dup 2 * 1 +              ( n*n+2n+1 )
                rot + swap
                1 +                     ( n+1 )
        repeat
        swap drop
        r> drop
        ;


( example 14.      radix for number conversions )

decimal

( : decimal       10 base! ; )
( : hex           16 base! ; )
: octal         8 base!  ;
: binary        2 base!  ;

( try converting numbers among different radices:

        decimal 12345 hex  .
        hex abcd decimal  .
        decimal 100 binary  .
        binary 101010101010 decimal  .

real programmers impress on novices by carrying a hp calculator
which can convert numbers between decimal and hexadecimal.  a
forth computer has this calculator built in, besides other functions.
)


( example 15.      ascii character table )

: character ( n -- )
        dup emit hex dup 3 .r
        octal dup 4 .r
        decimal 3 .r
        2 spaces
        ;

: line ( n -- )
        cr
        5 for   dup character
                16 +
        next
        drop ;

: table ( -- )
        32
        15 for  dup line
                1 +
        next
        drop ;


( example 16.  random numbers

random numbers are often used in computer simulations and computer
games.  this random number generator was published in leo brodie's
'starting forth'.
)

variable rnd                            ( seed )
here rnd !                              ( initialize seed )

: random ( -- n, a random number within 0 to 65536 )
        rnd @ 31421 *                   ( rnd*31421 )
        6927 +                          ( rnd*31421+6926, mod 65536)
        dup rnd !                       ( refresh he seed )
        ;

: choose ( n1 -- n2, a random number within 0 to n1 )
        random *                      ( n1*random to a double product)
        swap drop                       ( discard lower part )
        ;                                ( in fact divide by 65536 )

( to test the routine, type

        100 choose .
        100 choose .
        100 choose .

and varify that the results are randomly distributed betweem 0 and
99 . )


( example 17.      guess a number )
( example 16 must be loaded.)

variable myNumber
variable yourNumber

: limit ( n -- )
	yourNumber !
	cr ." Now, type you guess as:"
	cr ." xxxx guess"
	cr ." where xxxx is your guess."
	yourNumber @ choose myNumber !
        ;

: guess ( n1 -- , allow player to guess, exit when the guess is correct )
	myNumber @ 2dup =                  ( equal? )
        if      2drop           ( discard both numbers )
                cr ." correct!!!"
                exit
        then
        > if    cr ." too high!"
        else    cr ." too low."
        then    cr ." guess again?"
        ;

: greet ( -- )
        cr cr cr ." guess a number"
        cr ." this is a number guessing game.  i'll think"
        cr ." of a number between 0 and any limit you want."
        cr ." (it should be smaller than 32000.)"
        cr ." then you have to guess what it is."
	cr
	cr ." Set up the limit by typing:"
	cr ." xxxx limit "
	cr ." where xxxx is a number smaller than 32000."
        ;

( type 'greet' to start the game and the computer will entertain
  you for a while.  Use Forth interpreter for interaction with you.
 )
( greet )


